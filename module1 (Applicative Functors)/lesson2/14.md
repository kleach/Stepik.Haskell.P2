#### 1.2.14
#### Question
Двойственный оператор аппликации `(<**>)` из модуля `Control.Applicative` изменяет направление вычислений, не меняя порядок эффектов:
```haskell
infixl 4 <**>
(<**>) :: Applicative f => f a -> f (a -> b) -> f b
(<**>) = liftA2 (flip ($))
```
Определим оператор `(<*?>)` с той же сигнатурой, что и у `(<**>)`, но другой реализацией:
```haskell
infixl 4 <*?>
(<*?>) :: Applicative f => f a -> f (a -> b) -> f b
(<*?>) = flip (<*>)
```
Для каких стандартных представителей класса типов `Applicative` можно привести цепочку аппликативных вычислений, дающую разный результат в зависимости от того, какой из этих операторов использовался?

В следующих шести примерах вашей задачей будет привести такие контрпримеры для стандартных типов данных, для которых они существуют. Следует заменить аппликативное выражение в предложении `in` на выражение того же типа, однако дающее разные результаты при вызовах с  `(<??>) = (<**>)` и `(<??>) = (<*?>)`. Проверки имеют вид  `exprXXX (<**>) == exprXXX (<*?>)`  для различных имеющихся  `XXX`. Если вы считаете, что контрпримера не существует, то менять ничего не надо.
#### Solution
```haskell
import Control.Applicative ((<**>),ZipList(..))

infixl 4 <*?>
(<*?>) :: Applicative f => f a -> f (a -> b) -> f b
(<*?>) = flip (<*>)

exprMaybe :: (forall a b . Maybe a -> Maybe (a -> b) -> Maybe b) -> Maybe Int
exprMaybe op =
    let (<??>) = op
        infixl 4 <??>
    in Just 5 <??> Just (+2)

exprList :: (forall a b . [a] -> [a -> b] -> [b]) -> [Int]
exprList op =
    let (<??>) = op
        infixl 4 <??>
    in [1,2] <??> [(+3),(*4)]

exprZipList :: (forall a b . ZipList a -> ZipList (a -> b) -> ZipList b) -> ZipList Int
exprZipList op =
    let (<??>) = op
        infixl 4 <??>
    in ZipList [1,2] <??> ZipList [(+3),(+4)]

exprEither :: (forall a b . Either String a -> Either String (a -> b) -> Either String b) -> Either String Int
exprEither op =
    let (<??>) = op
        infixl 4 <??>
    in Left "AA" <??> Left "BB"

exprPair :: (forall a b . (String,a) -> (String,a -> b) -> (String,b)) -> (String,Int)
exprPair op =
    let (<??>) = op
        infixl 4 <??>
    in ("AA", 3) <??> ("BB",(+1))

exprEnv :: (forall a b . (String -> a) -> (String -> (a -> b)) -> (String -> b)) -> (String -> Int)
exprEnv op =
    let (<??>) = op
        infixl 4 <??> 
    in length <??> (\_ -> (+5))
```
